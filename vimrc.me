" vim: ft=vim
" ███████╗██╗  ██╗   ██╗███╗   ███╗ █████╗ ███╗   ██╗
" ██╔════╝██║  ╚██╗ ██╔╝████╗ ████║██╔══██╗████╗  ██║
" █████╗  ██║   ╚████╔╝ ██╔████╔██║███████║██╔██╗ ██║
" ██╔══╝  ██║    ╚██╔╝  ██║╚██╔╝██║██╔══██║██║╚██╗██║
" ██║By   ███████╗██║   ██║ ╚═╝ ██║██║  ██║██║ ╚████║
" ╚═╝ROBIN╚══════╝╚═╝   ╚═╝     ╚═╝╚═╝  ╚═╝╚═╝  ╚═══╝

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
""""   UNIVERSAL VIM OPTIONS THAT WE ARE ALL AGREED  """"""
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
set nocompatible

set mouse=a

" Ignore case when searching
set ignorecase

" When searching try to be smart about cases
set smartcase

" Highlight search results
set hlsearch

" Makes search act like search in modern browsers
set incsearch

" Show matching brackets when text indicator is over them
set showmatch

" Configure backspace so it acts as it should act
set backspace=eol,start,indent
set whichwrap+=<,>,h,l

" Turn on the Wild menu
set wildmenu

" Be smart when using tabs ;)
set smarttab

" 1 tab == 4 spaces
set shiftwidth=8
set tabstop=8

" Use spaces instead of tabs
set expandtab

set autoindent 
set smartindent

set showcmd      "Always print current keystroke
set ruler        "Always show current position

" Work with longline
set display+=lastline

set encoding=utf8

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => General Options
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Sets how many lines of history VIM has to remember
set history=500

" Enable filetype plugins
filetype plugin on
filetype indent on

" Set to auto read when a file is changed from the outside
set autoread

" Use Unix as the standard file type
set ffs=unix,dos,mac

language en_US.UTF-8

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => VIM user interface element
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Avoid garbled characters in Chinese language windows OS
let $LANG='cn'
set langmenu=cn
source $VIMRUNTIME/delmenu.vim
source $VIMRUNTIME/menu.vim

" A buffer becomes hidden instead of dropped
set hidden
set confirm

" Don't redraw while executing macros (good performance config)
set lazyredraw

" How many tenths of a second to blink when matching brackets
set mat=2

" No annoying sound on errors
set noerrorbells
set novisualbell
set t_vb=
set tm=500

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => files, backups and undo
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" turn backup off, since most stuff is in svn, git et.c anyway...
set nobackup
set nowb
set noswapfile

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Turn persistent undo on
"    means that you can undo even when you close a buffer/vim
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
try
    set undodir=~/.vim/temp_dirs/undodir
    set undofile
catch
endtry

" Ignore compiled files
set wildignore=*.o,*~,*.pyc
if has("win16") || has("win32")
    set wildignore+=.git\*,.hg\*,.svn\*
else
    set wildignore+=*/.git/*,*/.hg/*,*/.svn/*,*/.DS_Store
endif

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Buffers, Split Windows, Tabs(Tabline)
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Specify the behavior when switching/opening new buffers
if v:version >= 740
    set switchbuf=useopen,usetab
endif
" Always Show tabline
set showtabline=2
" Set Split Position
set splitright
set splitbelow

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Keyboard Feedback Configuration
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
set notimeout
set timeoutlen=500 " For <leader> mapping
set ttimeout
set ttimeoutlen=0  " No keycode dealy - no esc dealy
set scrolloff=0    " allow cursor to be at top and bottom
" set virtualedit=all "allow cursor to be anywhere

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Word Completion Popup Menu Configuration
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
set pumheight=25
inoremap <expr> <Tab>   pumvisible() ? "\<C-n>" : "\<Tab>"
inoremap <expr> <S-Tab> pumvisible() ? "\<C-p>" : "\<S-Tab>"

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Plugin Managerment
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
if empty(glob('~/.vim/autoload/plug.vim')) && !has('win32') && executable('curl')
    silent !mkdir -p ~/.vim/temp_dirs/undodir > /dev/null 2>&1
    silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim > /dev/null 2>&1
    if v:shell_error == 0
        autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
    endif
endif

" for windows oni or neovim in windows platform using powershell.
" 1. download git 2.run script below 3. run ` PlugInstall --sync | source $MYVIMRC ` Afterward
if 0
    md ~\AppData\Local\nvim\autoload
    $uri = 'https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim'
    (New-Object Net.WebClient).DownloadFile( $uri, $ExecutionContext.SessionState.Path.GetUnresolvedProviderPathFromPSPath( "~\AppData\Local\nvim\autoload\plug.vim"))
endif

if executable('nvim') && empty(glob('~/.config/nvim/init.vim')) && !has('win32')
    silent! !mkdir -p ~/.config
    silent! !ln -sf ~/.vim ~/.config/nvim
    silent! !ln -sf ~/.vimrc ~/.config/nvim/init.vim
endif

if has('nvim')
    set viminfo+=n~/.vim/.nviminfo
else
    "nviminfo is not compatable with viminfo
    set viminfo+=n~/.vim/.viminfo
    set ttymouse=xterm2 "| if $TMUX=="" | set ttymouse=xterm | endif
endif

" PLUGIN MANAGER START {{
if !empty(glob('~/.vim/autoload/plug.vim'))

    call plug#begin('~/.vim/plugged')

    """"""""""""""""""""""""""""""
    Plug 'mhinz/vim-startify'
    """"""""""""""""""""""""""""""

    """"""""""syntax and colorscheme"""""""""""""""""""
    Plug 'ErichDonGubler/vim-sublime-monokai'
    Plug 'sheerun/vim-polyglot'
    Plug 'justinmk/vim-syntax-extra'
    Plug 'vim-scripts/Super-Shell-Indent'
    """"""""""""""""""""""""""""""""""""""""""""""""""

    """"""""""""""""""""""""""""""
    " VIM's OPERATOR/TEXT OBJECT "
    """"""""""""""""""""""""""""""
    Plug 'tpope/vim-commentary', { 'on': '<Plug>Commentary' }
    """"""""""""""""""""""""""""""
    Plug 'wellle/targets.vim'
    """"""""""""""""""""""""""""""

    """""""""""""""""""""""""""""""
    "Plug 'natebosch/vim-lsc'
    "let g:lsc_server_commands = {
    "            \ 'python' : 'pyls',
    "            \ }
    "let g:lsc_enable_diagnostics = 0

    """ Language Server Installation hint:
    """ Python: pip install 'python-language-server[all]'
    """""""""""""""""""""""""""""

    """""""""""""""""""""""""""""" "On demand loading
    Plug 'chrisbra/Colorizer', { 'on': [ 'ColorHighlight', 'ColorToggle' ] }
    """"""""""""""""""""""""""""""

    """"""""""""""""""""""""""""""
    Plug 'majutsushi/tagbar', {'on': 'TagbarToggle'}
    """"""""""""""""""""""""""""""

    """"""""""""""""""""""""""""""
    Plug 'mbbill/undotree', { 'on': 'UndotreeToggle' }
    let g:undotree_CustomUndotreeCmd = 'vertical aboveleft 32 new'
    let g:undotree_CustomDiffpanelCmd= 'belowright 12 new'
    """"""""""""""""""""""""""""""

    """"""""""""""""""""""""""""""
    ":h easyalign "check the helpdoc line 468 after load the plugin
    Plug 'junegunn/vim-easy-align', { 'on': ['<Plug>(EasyAlign)','EasyAlign']}
    """"""""""""""""""""""""""""""

    """"""""""""""""""""""""""""""
    Plug 'junegunn/goyo.vim', {'on': 'Goyo'}
    let g:goyo_width=130
    """"""""""""""""""""""""""""""

    """"""""""""""""""""""""""""""
    Plug 'tpope/vim-fugitive', {'on': ['Gwrite','Git']}
    """"""""""""""""""""""""""""""

    call plug#end()
    "}} PLUGIN MANAGER END 
endif

silent! packadd! matchit

"""""""""""""""""""""""""""""""""""""""""
""LEADER KEY MAPPING"
"""""""""""""""""""""""""""""""""""""""""
let mapleader = "\<space>"

"""""""""""""""""""""""""""""""""""""""""
""PLUGIN LEADER KEY MAPPING"
"""""""""""""""""""""""""""""""""""""""""
" easy-alignment no argument go to interactive mode
vmap <leader>a    :EasyAlign
nmap <leader>u    :UndotreeToggle<cr>
map  <leader>m    :TagbarToggle<cr>
nmap <silent>     <leader>z  :Goyo<cr>:normal! zz<cr>
map  gc           <Plug>Commentary
nmap gcc          <Plug>CommentaryLine

"""""""""""""""""""""""""""""""""""""""""
"" All leader key mapping
"""""""""""""""""""""""""""""""""""""""""
" Go to Buffer
nmap <leader>b :ls<cr>:buffer

" quickfix window  displaying
map <leader>v  :botright copen<cr>

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => General Vim Editor Setup
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
nmap <silent> <leader>l :silent! set nolist <bar> silent! set number!<cr>

"Join the line below with space
" nnoremap <leader>j  J
" Reverse of J
nnoremap <leader>j v$hdO<Esc>pj
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
""Join the line below with space => Code Development - TagBar
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Switch CWD to the directory of the open buffer
map <leader>cd :cd %:p:h<cr>:pwd<cr>

" Disable highlight when <space><enter> is pressed
nnoremap <silent> <expr> <cr> &buftype ==# 'quickfix' ? "\<cr>" : ":noh<cr>"

" Esc, Ctrl, :, ^W, ^s, Tab, Alt
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Spell checking
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
map <leader>sp :setlocal spell!<cr>
"  z= provide a list of replacement
"  zg add word to definition
"  ]s next spell error
"  [s previous spell error

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Misc
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Remove the Windows ^M - when the encodings gets messed up
" noremap <leader>m mmHmt:%s/<C-V><cr>//ge<cr>'tzt'm

" Repeat last typed command
nnoremap <leader>; @:

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Fast editing and reloading of vimrc configs
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
map <leader>ee :e! ~/.vimrc<cr>
map <leader>et :e! ~/.tmux.conf<cr>
map <leader>ez :e! ~/.zshrc<cr>

noremap <leader>r :source $MYVIMRC<cr>

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" END of Leader Key mapping
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Extra Normal Mode Mapping 
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Remap = to line end $, like it, easier to press and remember
noremap \ gg=G''
noremap = $
noremap <nowait> q :q<cr>
noremap <nowait> Q q

" for practicing vim way
" nnoremap <silent> <up>    <NOP>
" nnoremap <silent> <down>  <NOP>
" nnoremap <silent> <left>  <NOP>
" nnoremap <silent> <right> <NOP>

" Quickly add empty lines
nnoremap [<space>  :<c-u>put! =repeat(nr2char(10), v:count1)<cr>
nnoremap ]<space>  :<c-u>put =repeat(nr2char(10), v:count1)<cr>

" Quickly move current line
nnoremap [e  :<c-u>execute 'move -1-'. v:count1<cr>
nnoremap ]e  :<c-u>execute 'move +'. v:count1<cr>

" To go to the next/previous quickfix/linter entry
nnoremap <silent> [p :cprevious<cr>zz 
nnoremap <silent> ]p :cnext<cr>zz

" To go to the next/previous quickfix/linter entry in a different file
nnoremap <silent> [l :cpf<cr>zz 
nnoremap <silent> ]l :cnf<cr>zz

" TODO map shift-tab to cycle through jumps for differen file
nnoremap <silent> <Tab> <C-o>
nnoremap <silent> <S-Tab> <C-i>

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => InertMode/CMDline Editing mappings
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Additionaly you can use c-f to editing cmd in normal mode window
cnoremap <C-A> <Home>
cnoremap <C-E> <End>
cnoremap <C-Z> <S-Left>
cnoremap <C-S> <S-Right>
cnoremap <C-D> <S-Right><C-W>
cnoremap <C-P> <Up>
cnoremap <C-N> <Down>
cnoremap <C-Y> <C-R>"

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Terminal mode mappings
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
if v:version >= 801
    tnoremap <Esc> <c-\><c-n>
endif
" :W sudo saves the file
" (useful for handling the permission-denied error)
command! W w !sudo tee % > /dev/null

""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Pattern Match, Search Highlight
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Normal mode pressing * or # searches for word under cursor
" enable * # for visual selected text
" whitespace match any whitespace in potential result
let s:save_cpo = &cpo | set cpo&vim
function! s:VSetSearch(cmd)
    let old_reg = getreg('"')
    let old_regtype = getregtype('"')
    normal! gvy
    if @@ =~? '^[0-9a-z,_]*$' || @@ =~? '^[0-9a-z ,_]*$'
        let @/ = @@
    else
        let pat = escape(@@, a:cmd.'\')
        let pat = substitute(pat, '\n', '\\n', 'g')
        let @/ = '\V'.pat
    endif
    normal! gV
    call setreg('"', old_reg, old_regtype)
endfunction
vnoremap <silent> * :<C-U>call <SID>VSetSearch('/')<cr>/<C-R>/<cr>
vnoremap <silent> # :<C-U>call <SID>VSetSearch('?')<cr>?<C-R>/<cr>
vmap <kMultiply> *
let &cpo = s:save_cpo | unlet s:save_cpo

" Delete trailing white space on save, useful for some filetypes ;)
func! CleanExtraSpaces()
    let save_cursor = getpos(".")
    let old_query = getreg('/')
    silent! %s/\s\+$//e
    call setpos('.', save_cursor)
    call setreg('/', old_query)
endfun

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
""""""""        Utility Functions    """"""""""""""""""
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" userful to redirect internal command output to paste buffer
func! Exec(command)
    redir =>output
    silent exec a:command
    redir END
    return output
endfunct!

" returns the binary string representation of a number.
function! Nr2Bin(nr)
    let n = a:nr
    let r = ""
    while n
        let r = '01'[n % 2] . r
        let n = n / 2
    endwhile
    return r
endfunc

function! TransBackground()
    hi Normal ctermbg=NONE guibg=NONE
    hi NonText ctermbg=NONE guibg=NONE
    hi clear CursorLine
    hi CursorLine gui=underline cterm=underline ctermfg=NONE guifg=NONE
endfunction

command! CloseHiddenBuffers call s:CloseHiddenBuffers()
function! s:CloseHiddenBuffers()
    let open_buffers = []
    for i in range(tabpagenr('$'))
        call extend(open_buffers, tabpagebuflist(i + 1))
    endfor
    for num in range(1, bufnr("$") + 1)
        if buflisted(num) && index(open_buffers, num) == -1
            exec "bdelete ".num
        endif
    endfor
endfunction

iab xdate  <c-r>=strftime("%d/%m/%y %H:%M:%S")<cr>
iab xdate2 <c-r>=strftime("%F")<cr>

" :HeadPager
" Turn vim into a pager with a header row
" Adapted from https://unix.stackexchange.com/a/27840/143088
fun! HeadPager()
    " If you didn't get three lines, shortcut out
    if line('$') < 3
        set nocompatible
        nmap <silent> q :qa!<c-M>
        nmap <silent> Q :qa!<c-M>
        return
    endif

    set noswapfile
    set nocompatible
    set nowrap
    set scrollopt=hor
    set scrollbind

    " Hide statusline in lower window
    set laststatus=0
    " Explain mapped chars in status line.
    set statusline=\ \ \ Q\ to\ quit\.\ Arrows\ or\ mousewheel\ to\ scroll\.\ \(Vim\ commands\ work\,\ too\.\)

    " Delete/copy header lines
    silent execute '1,2d'

    " Split screen with new buffer (opens at top)
    execute 'new'

    " Switch to upper split
    wincmd k

    " Paste the header over the blank line
    execute 'norm! Vp'

    " Header highlighting
    syn match Pipe "|"
    hi def Pipe ctermfg=blue
    syn match Any /[^|]\+/
    hi def Any ctermfg=yellow

    " Switch back to lower split for scrolling
    wincmd j

    " Set lower split height to maximum
    execute "norm! \<c-W>_"

    " Syntax highlighting
    syn cluster CellContents contains=None
    syn match Pipe "|" contained nextgroup=@CellContents skipwhite
    hi def Pipe ctermfg=blue

    " Start with newline or |. End right before next | or EOL
    syn region Cell start=/\v(^|\|)\s*/ end=/\v(\||$)\@=/ contains=LineNumber,Pipe

    syn match NumPos /\v\+?\d+(,?\d{3})*\.?\d*\ze *(\||$)\@=/ contained
    syn match NumNeg   /\v-\d+(,?\d{3})*\.?\d*\ze *(\||$)\@=/ contained
    syn match NumZero         /\v[+-]?0+\.?0*\ze *(\||$)\@=/  contained
    hi def NumPos ctermfg=cyan
    hi def NumNeg ctermfg=red
    hi def NumZero ctermfg=NONE
    syn cluster CellContents add=NumPos,NumNeg,NumZero

    syn match DateVal /\v\d{4}-\d{2}-\d{2}/ contained nextgroup=TimeVal skipwhite
    syn match TimeVal /\v\d{1,2}:\d{2}(:\d{2})?(\.\d+)?(Z| ?\c[AP]M)?\ze *(\||$)\@=/ contained
    hi def DateVal ctermfg=magenta
    hi def TimeVal ctermfg=magenta
    syn cluster CellContents add=DateVal,TimeVal

    syn match TrueVal /\v\c(t(rue)?|y(es)?)\ze *(\||$)\@=/ contained
    syn match FalseVal /\v\c(f(alse)?|no?)\ze *(\||$)\@=/ contained
    hi def TrueVal ctermfg=green
    hi def FalseVal ctermfg=red
    syn match NullVal /\v\cnull?\ze *(\||$)\@=/ contained
    hi def NullVal ctermbg=gray ctermfg=black
    syn cluster CellContents add=TrueVal,FalseVal,NullVal

    syn match LineNumber /^ *\d\+/ contained
    hi def LineNumber ctermfg=yellow

    " Arrows do scrolling instead of moving
    nmap <silent> <Up> 3<c-Y>
    nmap <silent> <Down> 3<c-E>
    nmap <silent> <Left> zH
    nmap <silent> <Right> zL
    nmap <Space> <PageDown>
    " Faster quit (I tend to forget about the upper panel)
    nmap <silent> q :qa!<c-M>
    nmap <silent> Q :qa!<c-M>

    " Ignore external updates to the buffer
    autocmd! FileChangedShell */fd/*
    autocmd! FileChangedRO */fd/*
endfun
command! -nargs=0 HeadPager call HeadPager()

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => Code Navigation - Cscope/LanguageClient
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
if has("cscope")
    set cscopequickfix=g-,s-,c-,f-,i-,t-,d-,e-
    " add any cscope database in current directory
    map <leader>ca :cs add ~/414gr1_make/target/sources/sto/cscope.out ~/414gr1_make/target/sources/sto<cr>
    " show msg when any other cscope db added
    set cscopeverbose
    " seach for definition
    map <leader>gd  :cs find g <c-r><c-w><cr>
    " search for this symbol
    map <leader>gs  :cs find s <c-r><c-w><cr>
    " find the file under the name
    map <leader>gf  :cs find f <c-r><c-p><cr>
    " search files that include this file
    map <leader>gi  :cs find i <c-r>=expand("%:t")<cr><cr>
    " egrep pattern matching
    map <leader>ge  :cs find e <c-r><c-w><cr>
    " Find assignments to this symbol
    map <leader>ga  :cs find a <c-r><c-w><cr>
    " search functions that call this function
    map <leader>gc  :cs find c <c-r><c-w><cr>
    " search this string
    map <leader>gt  :cs find t <c-r><c-w><cr>
endif

" nnoremap <silent> <leader>gk :LSClientShowHover<cr>
" nnoremap <silent> <leader>gd :LSClientGoToDefinition<cr>
" nnoremap <silent> <leader>gr :LSClientFindReferences<cr>
" nnoremap <silent> <leader>gs :LSClientDocumentSymbol<cr>
" nnoremap <silent> <leader>gn :LSClientRename<cr>

"""""""""""""""""""""""""""""""""""""""""
"" Code/Text AutoFormat,AutoComplete
"""""""""""""""""""""""""""""""""""""""""
function! SetupCodeEnvironment()
    "set trigger for language-client's omnifunc
    if &filetype == 'c' || &filetype == 'cpp'
        if executable('clang-format')
            if empty(glob('~/.clang-format'))
                setlocal formatprg=clang-format\ --style='Webkit'
                setlocal equalprg=clang-format\ --style='Webkit'
            else
                setlocal formatprg=clang-format\ --style='file'
                setlocal equalprg=clang-format\ --style='file'
            endif
        endif
    elseif &ft  == 'javascript'
        if executable('js-beautify')
            " setlocal equalprg='js-beautify '
        endif
    elseif &ft  == 'python'
        if executable('pyls')
            setlocal makeprg=python\ %
            setlocal equalprg=autopep8\ --in-place\ --aggressive
        endif
    else
    endif
endfunction

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" => colors , fonts, display, highlight
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
function! DisplayReloadTheme()
    try 
        colorscheme sublimemonokai
    catch
        silent! colorscheme default
    endtry
    " Set the Syntax
    syntax on
    set cursorline
    if $TERM  =~? '256color'
        set t_Co=256
        if g:colors_name == "sublimemonokai"
            if $COLORTERM =~? 'truecolor'
                set termguicolors
            endif
        endif
    endif
    set number
    " show hidden chars using shortcuts
    set listchars=tab:␉·,eol:␤,nbsp:▓
    set nolist
    " two highlight group nontext & specialkey for listchars
    highlight nontext ctermfg=238 guifg=#414141
    " override color scheme for the listchars "#649a9a
    highlight CursorLine guibg=#404040 gui=bold cterm=bold ctermbg=234

    highlight QuickFixLine term=reverse ctermbg=254
    highlight QuickFixLine gui=reverse guibg=#000000

    highlight Search cterm=bold ctermbg=Yellow ctermfg=Black
    highlight Search gui=bold guibg=Brown guifg=White

    set laststatus=2 "In order to show the good statusline
    set showcmd      "Always print current keystroke
    set ruler        "Always show current position
    set cmdheight=1  "Height of the command bar
    set statusline=
    set statusline+=\ %(第%c列%) 
    set statusline+=\ %f\ %r%w         " filename and flags
    set statusline+=\ %(共%L行%) 
    set statusline+=%=%<      " force space and start cut if too long
    set statusline+=\ [%{getcwd()}][%{&ft}]    " flags and filetype
    set statusline+=\ [UTF:%B][Buf:%n]\  " unicode under cursor

    highlight Statusline cterm=bold ctermfg=red ctermbg=235
    highlight Statusline gui=bold guifg=black guibg=#b0dfe5

    highlight StatusLineNC cterm=NONE ctermfg=59 ctermbg=235 
    highlight StatuslineNC gui=NONE guibg=#64645e guifg=#75715E

    highlight TabLine cterm=NONE ctermfg=252 ctermbg=239 
    highlight TabLine gui=NONE guifg=black guibg=#555555 

    highlight TabLineSel cterm=bold ctermfg=white ctermbg=black
    highlight TabLineSel gui=bold guifg=red guibg=#36454F

    highlight TabLineFill cterm=bold ctermfg=243 ctermbg=239 
    highlight TabLineFill gui=NONE guifg=#8F908A guibg=#555555
    " Customer User Color %1*text%0* 
    " highlight User1 cterm=NONE ctermfg=59 ctermfg=235
    " highlight User1 gui=bold guifg=red guibg=#b0dfe5
endfunction

call DisplayReloadTheme()
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
""""""""""""""""    AUTO COMMANDS         """""""""""""""""
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
if has("autocmd")
    augroup myvimrc
        autocmd! *

        " Auto reload conffiguration file, clean whitespace for some common code files
        autocmd BufWritePost ~/.vimrc nested source ~/.vimrc | :call DisplayReloadTheme()
        autocmd BufWritePre *.txt,*.js,*.py,*.wiki,*.sh,*.coffee :call CleanExtraSpaces()

        " Return to last edit position when opening files (You want this!)
        autocmd BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g'\"" | endif

        " Save the code folding if we had one
        " autocmd BufWinLeave * silent! mkview
        " autocmd BufWinEnter * silent! loadview

        " Properly disable sound on errors on MacVim
        if has("gui_macvim")
            autocmd GUIEnter * set vb t_vb=
        endif

        if v:version >= 800
            " auto save 
            autocmd TextChanged * let v:errmsg = '' | silent! write | if v:errmsg == '' | write | endif
            autocmd InsertLeave * let v:errmsg = '' | silent! write | if v:errmsg == '' | write | endif
            " Focus: only work in GUI or under tmux + vim-tmux-focus plugin
            " autocmd FocusGained * :call DisplayReloadTheme()
            " autocmd FocusLost   * :highlight Normal ctermbg=0 guibg=#101010
        endif

        " smart cursorline
        autocmd WinEnter * setlocal cursorline
        autocmd WinLeave * setlocal nocursorline

        " tab special for makefile 
        autocmd FileType make setlocal noexpandtab tabstop=8 shiftwidth=8

        autocmd FileType * :call SetupCodeEnvironment()

    augroup END
endif

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"""""""""""""    END OF VIM OPTIONS SETTING    """"""""""""
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
